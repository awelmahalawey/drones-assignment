plugins {
	id 'org.springframework.boot' version '2.7.4'
	id 'io.spring.dependency-management' version '1.0.14.RELEASE'
	id 'java'
	id "org.openapi.generator" version "5.3.0"
}

group = 'com.musala.soft'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	implementation 'org.openapitools:jackson-databind-nullable:0.2.3'
	implementation 'javax.validation:validation-api:2.0.1.Final'
	implementation group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Open API Generator configuration
openApiGenerate {
	generatorName = "spring"
	inputSpec = "$rootDir/src/main/resources/openApiGenConfig/drones-OAPI.yaml".toString()
	outputDir = "$buildDir/generated".toString()
	apiPackage = "com.musala.soft.drones.api"
	invokerPackage = "com.musala.soft.drones.invoker"
	modelPackage = "com.musala.soft.drones.model"
	ignoreFileOverride = "$rootDir/.openapi-generator-ignore"
	configOptions = [
			dateLibrary: "java8"
	]
	globalProperties = [
			apis: "",
			models: "",
			supportingFiles: "ApiUtil.java"
	]
}

// Enforcing task OpenAPI Generator to run while compiling project.
compileJava.dependsOn tasks.openApiGenerate
// Adding generated files by OpenApi Generator to the source files.
sourceSets.main.java.srcDir "$buildDir/generated/src/main/java"
sourceSets.main.resources.srcDir "$buildDir/generated/src/main/resources"
